# Maintainer: Andreas Innerlohninger <innerand at nxa dot at>
#
# On the basis of the xkeyboard-config PKGBUILD
# by Andreas Radke and Jan de Groot

pkgname=xkeyboard-config-umlauts
pkgver=2.20
pkgrel=1
pkgdesc="X keyboard configuration files, gb layout with umlauts"
arch=(any)
license=('custom')
url="https://www.freedesktop.org/wiki/Software/XKeyboardConfig"
makedepends=('intltool' 'xorg-xkbcomp' 'xorg-server-devel' 'libxslt' 'git')
provides=('xkbdata' 'xkeyboard-config' )
replaces=('xkbdata' 'xkeyboard-config' )
conflicts=('xkbdata' 'xkeyboard-config' )
source=("git+https://anongit.freedesktop.org/git/xkeyboard-config.git#tag=xkeyboard-config-${pkgver}" 'xkeyboard-config.patch')
sha256sums=('SKIP' '38bdebeb8fefd06dc5eb010c19fd337a56f4c4ce7c9e771630f1d138c1df0d2e')

prepare() {
  cd xkeyboard-config
  patch -p1 -i ../xkeyboard-config.patch
}

build() {
  cd xkeyboard-config
  ./autogen.sh
  ./configure --prefix=/usr \
      --with-xkb-base=/usr/share/X11/xkb \
      --with-xkb-rules-symlink=xorg \
      --enable-compat-rules=yes
  make
 }

 package() {
  cd xkeyboard-config

  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/share/X11/xkb/compiled"

  install -m755 -d "${pkgdir}/var/lib/xkb"
  install -m755 -d "${pkgdir}/usr/share/licenses/xkeyboard-config"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xkeyboard-config/"
}
